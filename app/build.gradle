apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.skyzone.netdemomvp"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'API_SERVER_URL', '"http://my.api/"'
        buildConfigField "boolean", "AUTO_UPDATES", "true"
    }

    signingConfigs {//签名的配置
        release {
            storeFile file("JaxDemo.jks")
            storePassword 'android'
            keyAlias 'NetDemoMVP'
            keyPassword 'android'
        }
    }

    buildTypes {

        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            debuggable true
            minifyEnabled true
            useProguard true
            //if need remove unless resource?note:must use shrinkResources with minifyEnabled
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

//            resValue "string", "loading", "jax_loading"

            applicationVariants.all { variant ->
                if (variant.buildType.name.equals('release')) {
                    variant.outputs.each { output ->
                        def outputFile = output.outputFile
                        if (outputFile != null && outputFile.name.endsWith('.apk')) {
                            def fileName = "jax_demo_v${defaultConfig.versionName}_${releaseTime()}_${variant.flavorName}.apk"
                            output.outputFile = new File(outputFile.parent, fileName)
                        }
                    }
                }
            }

            //针对很多渠道
            productFlavors.all { flavor ->
                manifestPlaceholders.put("UMENG_CHANNEL_VALUE", name)
            }
        }
    }

    productFlavors {//多渠道打包,命令行打包：gradlew assembleRelease
        xiaomi {
            applicationId 'com.skyzone.netdemomvpxiaomi'
            buildConfigField "boolean", "AUTO_UPDATES", "false"
//            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
//            manifestPlaceholders.put("UMENG_CHANNEL_VALUE", 'xiaomi')
        }
        //gradlew assembleGooglePlayRelease
        googleplay {
            applicationId 'com.skyzone.netdemomvpgoogleplay'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile rootProject.ext.dependencies["appcompat-v7"]
    compile rootProject.ext.dependencies["design"]
    //net
    compile rootProject.ext.dependencies["retrofit"]
    compile rootProject.ext.dependencies["converter-gson"]
    compile rootProject.ext.dependencies["adapter-rxjava"]
    compile rootProject.ext.dependencies["logging-interceptor"]

    compile rootProject.ext.dependencies["rxjava"]
    compile rootProject.ext.dependencies["rxandroid"]
    //inject
    compile rootProject.ext.dependencies["butterknife"]
    apt rootProject.ext.dependencies["butterknife-compiler"]
    //img
    compile rootProject.ext.dependencies["glide"]
    compile rootProject.ext.dependencies["glide-transformations"]
    //logger
    compile rootProject.ext.dependencies["xlog"]
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
